use lowlevel;
use parse::lexer::Token;

grammar<'input>;

// [1]	nquadsDoc	::=	statement? (EOL statement)* EOL?
pub nquads_doc: () = statement? ("EOL" statement)* "EOL"?;

// [2]	statement	::=	subject predicate object graphLabel? '.'
statement: lowlevel::Statement<'input> = <subject:subject> <predicate:predicate> <object:object> <graph:graph_label?> "." => lowlevel::Statement { <> };

// [3]	subject	::=	IRIREF | BLANK_NODE_LABEL
subject: lowlevel::Term<'input> = {
    "IRIREF" => lowlevel::Term::NamedNode { value: <> },
    "BLANK_NODE_LABEL" => lowlevel::Term::BlankNode { value: <> },
};

// [4]	predicate	::=	IRIREF
predicate: lowlevel::Term<'input> = "IRIREF" => lowlevel::Term::NamedNode { value: <> };

// [5]	object	::=	IRIREF | BLANK_NODE_LABEL | literal
object: lowlevel::Term<'input> = {
    "IRIREF" => lowlevel::Term::NamedNode { value: <> },
    "BLANK_NODE_LABEL" => lowlevel::Term::BlankNode { value: <> },
    literal
};

// [6]	graphLabel	::=	IRIREF | BLANK_NODE_LABEL
graph_label: lowlevel::Term<'input> = {
    "IRIREF" => lowlevel::Term::NamedNode { value: <> },
    "BLANK_NODE_LABEL" => lowlevel::Term::BlankNode { value: <> },
};

// [7]	literal	::=	STRING_LITERAL_QUOTE ('^^' IRIREF | LANGTAG)?
literal: lowlevel::Term<'input> = {
    <"STRING_LITERAL_QUOTE"> "^^" "IRIREF" => lowlevel::Term::Literal { value: <>, language: None, datatype: None },
    <"STRING_LITERAL_QUOTE"> "LANGTAG" => lowlevel::Term::Literal { value: <>, language: None, datatype: None },
    <"STRING_LITERAL_QUOTE"> => lowlevel::Term::Literal { value: <>, language: None, datatype: None },
};

extern {
    type Location = usize;

    enum Token<'input> {
        "EOL" => Token::EOL,
        "." => Token::Period,
        "^^" => Token::Carets,
        "IRIREF" => Token::IRIRef(<&'input str>),
        "LANGTAG" => Token::LangTag(<&'input str>),
        "STRING_LITERAL_QUOTE" => Token::StringLiteralQuote(<&'input str>),
        "BLANK_NODE_LABEL" => Token::BlankNodeLabel(<&'input str>),
        "COMMENT" => Token::Comment(<&'input str>),
    }
}
